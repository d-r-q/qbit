buildscript {
    ext.kotlinIoVersion = "0.1.10"
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.0'
    }
}

plugins {
    id 'kotlin-multiplatform' 
}

group 'org.qbit'
version '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

apply plugin: 'maven'
apply plugin: 'info.solidsoft.pitest'

pitest {
    targetClasses = ['qbit.*']  //by default "${project.group}.*"
    threads = 12
    outputFormats = ['HTML']
    timestampedReports = false
    mutators  = ['ALL']
}

test {
    jvmArgs '-ea'
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

kotlin {
    jvm {
        compilations.all {
            kotlinOptions {
                jvmTarget = '1.6'
            }
        }
    }
    
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin("stdlib")
                implementation kotlin("reflect")
                implementation "org.jetbrains.kotlinx:kotlinx-io:$kotlinIoVersion"
            }
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
        jvmMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
                implementation "org.jetbrains.kotlinx:kotlinx-io-jvm:$kotlinIoVersion"
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
    }
}